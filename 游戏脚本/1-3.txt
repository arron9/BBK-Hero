@银杏村

SETSCENENAME "银杏村"
SHOWSCENENAME
SET 1 13 @地图索引
if 3 击退半兽人入侵
if 2 半兽人入侵银杏村
callback

武器店:
SAY 0 "欢迎光临，有什么事情需要我帮忙的"
menu 200 "买武器 询问"
ifcmp 200 0 exitaddr
ifcmp 200 1 买武器
ifcmp 200 2 武器店询问
callback

买武器:
BUY "7001 7002 7003 7021 7005 7006 7007 7008 7009 7010 7011 7012 7013 7014 7015 7016 7017 7018 7019"
callback

武器店询问:
SAY 0 "少年，如果缺钱的话，矿工是一份很有前途的职业，买一把鹤嘴锄去北边的比奇矿区开始创业吧，传言，越是地下深处，越容易挖到高价值的矿石"
callback

exitaddr:
callback

衣服店:
SAY 0 "刀剑无眼，盔甲有情"
menu 200 "买衣服 询问"
ifcmp 200 0 exitaddr
ifcmp 200 1 买衣服
ifcmp 200 2 衣服店询问

callback

买衣服:
SAY 0 "本店铺还未开张"
callback

衣服店询问:
SAY 0 "天工神剪是我的师傅，听师兄弟说，师傅被沃玛教主囚禁了，不知道你能不能帮助我们找到我们的师傅。"
callback

药店:
SAY 0 "本店铺还未开张"
callback

当铺:
sale
callback

比奇省:
loadmap 1 4 59 72
startchapter 1 4
callback

告示牌:
message "银杏村"
callback

许愿树:
message "许愿树"
if 3 击退半兽人入侵
SHOWGUT 0 0 "当圣洁的眼泪滴落而下，破土而出的是许愿之花。无数次风雨如晦的漫漫等待，所希冀的不过是刹那芳华。湘妃竹下泣，断肠花上唏。日夜与君忆，但求相偎依。泪落地，树成栖。梦已既，此生息。玛法历1457年，半兽人入侵，比奇村被迫举村迁移。玛法历1477年，小队半兽人袭击，村里勇士死伤过半。玛法历1483年，阿兰种下许愿树。玛法历1485年，阿兰恋人――战士海也闯荡半兽人巢穴，功亏一篑，下落不明。阿兰痛心而亡。阿兰唯一的心愿就是找到她，希望有勇士帮她实现这个愿望。"
say 0 "阿兰的愿望啊，可恶的半兽人，我一定为你找到海也。"
setevent 1
callback


半兽人入侵银杏村:
CREATENPC 1 2 5 12
MOVE 1 9 12
CREATENPC 2 31 7 11
CREATENPC 3 31 8 11
CREATENPC 4 31 6 11 
say 0 "小男孩：半兽人来了，快跑啊！"
DELETENPC 1
say 0 "半兽人：竟敢杀我们半兽人，兄弟们，把这个村子洗虐掉！"
say 0 "住手！"
say 0 "半兽人：哈，又来一块新鲜肉。"
ENTERFIGHT 0 1 2 3 2 112 32 0 0 0 0 0 0 lossto 打败银杏村半兽人头目
callback

lossto:
gameover
callback

打败银杏村半兽人头目:
GAINGOODS 14 1
DELALLNPC
say 0 "半兽人：啊，可恶的人类。。。"
say 0 "你们半兽人残害生灵，注定会消亡！"
CREATENPC 6 2 9 11
MOVE 6 6 11
say 0 "小男孩：大哥哥，你真是个英雄！"
CLREVENT 2
callback

击退半兽人入侵:
callback

gutevent 41 武器店
gutevent 42 衣服店
gutevent 43 药店
gutevent 44 当铺
gutevent 45 比奇省
gutevent 46 告示牌
gutevent 47 许愿树
